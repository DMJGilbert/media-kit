import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_video'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.alexmercerind.media_kit_libs_android_video'
    }

    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}

task downloadDependencies(type: Exec) {   
        // Delete previously created /output subdirectory.

        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // Download all *.jar dependencies & verify their MD5 checksums.
        def filesToDownload = [
            ["url": "https://github.com/Samuel-Bowden/libmpv-android-video-build/releases/download/1.1.10/default-arm64-v8a.jar", "md5": "b16d42c059a28e61e05e2bcf94cd2c12", "destination": file("$buildDir/v1.1.10/default-arm64-v8a.jar")],
            ["url": "https://github.com/Samuel-Bowden/libmpv-android-video-build/releases/download/1.1.10/default-armeabi-v7a.jar", "md5": "04d5d713194b6100a4362796938756fb", "destination": file("$buildDir/v1.1.10/default-armeabi-v7a.jar")],
            ["url": "https://github.com/Samuel-Bowden/libmpv-android-video-build/releases/download/1.1.10/default-x86_64.jar", "md5": "8e0aa526676c45adaadeb95c797fe94e", "destination": file("$buildDir/v1.1.10/default-x86_64.jar")],
            ["url": "https://github.com/Samuel-Bowden/libmpv-android-video-build/releases/download/1.1.10/default-x86.jar", "md5": "036a4f408c57315b66a9bb0b29b4b63a", "destination": file("$buildDir/v1.1.10/default-x86.jar")]
        ]

        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination
            if (destFile.exists()) {
                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedMD5 != fileInfo.md5) {
                    destFile.delete()
                    println "MD5 mismatch. File deleted: ${destFile}"
                }

            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }

                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedMD5 != fileInfo.md5) {
                    throw new GradleException("MD5 verification failed for ${destFile}")
                }
            }

            copy {
                from destFile
                into outputDir
            }
        }
}

assemble.dependsOn(downloadDependencies)
